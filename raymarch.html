<!DOCTYPE html>
<html>
<head>
        <meta charset="utf-8" />
        <title>Raymarching</title>
        <link rel="shortcut icon" href="favicon.ico" type="image/x-icon">
        <style>
            body, html {margin:0; width: 100%; height: 100%; min-height: 400px; min-width: 600px; overflow-y: hidden;}
            .vessel {
            	width: 100%;
            	height: 100%;
            }
        </style>
</head>
<body>

	<canvas class="vessel"></canvas>

<script class="vsh" type="x-shader/x-fragment">

	attribute vec2 a_position;

	void main(void){
		gl_Position = vec4(a_position, 0.0, 1.0);
	}

</script>
<script class="fsh" type="x-shader/x-fragment">

	precision mediump float;
	uniform float u_time;
	uniform vec2 u_size;
	uniform vec2 u_mouse;

	const int MAX_STEPS = 512;
	const float MIN_DST = 0.0;
	const float MAX_DST = 100.0;
	const float EPS = 0.0001;

	float blend( float a, float b, float k )
	{
	    float h = clamp( 0.5 + 0.5 * (b - a) / k, 0.0, 1.0);
	    return mix(b, a, h) - k * h * (1.0 - h);
	}

	float sphereSdf(vec3 p, vec3 c, float r){
		return length(p - c) - r;
	}

	float cubeSdf(vec3 point, vec3 sides){

		vec3 d = abs(point) - sides;

		float ins = min(max(d.x, max(d.y, d.z)), 0.0);
		float outs = length(max(d, 0.0));
  		return ins + outs - .005;

	}

	float sdf(vec3 point){

		float sd = sphereSdf(point, vec3(0., sin(u_time / 2.) * .2 + .44, 0.), .2);
		//float sd2 = sphereSdf(point, vec3(0.0, 0.0, 0.0), .4);
		float cd = cubeSdf(point, vec3(.3, .3, .3));

		float b1 = blend(sd, cd, .16);
		//float b2 = blend(sd2, cd, .32);

		return b1;

	}

	vec3 getNormal(vec3 p){
		return normalize(vec3(
        	sdf(vec3(p.x + EPS, p.y, p.z)) - sdf(vec3(p.x - EPS, p.y, p.z)),
        	sdf(vec3(p.x, p.y + EPS, p.z)) - sdf(vec3(p.x, p.y - EPS, p.z)),
        	sdf(vec3(p.x, p.y, p.z  + EPS)) - sdf(vec3(p.x, p.y, p.z - EPS))
    	));
	}

	vec3 getAim(float fov, vec2 size, vec2 coord){

		vec2 xy = coord - size / 2.0;
		float z = size.y / tan(radians(fov) / 2.0);

		return normalize(vec3(xy, -z));

	}

	float shadow(vec3 o, vec3 p, float d){

		float t = MIN_DST + EPS * 100.;
		float final = 1.0;
		float prev = 1e16;

		for (int i = 0; i < MAX_STEPS; i++){

			float h = sdf(o + t * p);
			if (h < EPS) return 0.0; else if (h > d) break;

			float k = h * h / (2.0 / prev);
			float l = sqrt(h * h - k * k);
			final = min(final, 2.0 * l / max(0.0, t - k));
			prev = h;
			t += h;

		}

		return final;

	}

	vec3 getPhong(vec3 p, vec3 o, vec3 light, vec3 lightColor, vec3 N){

		vec3 L = normalize(light - p);
		vec3 V = normalize(o - p);
		vec3 R = normalize(reflect(-L, N));

		vec3 diffuse = vec3(0.6, 0.9, 0.6);

		float dotLN = dot(L, N);
		float dotRV = dot(R, V);

		if (dotLN < 0.0) return vec3(0., 0., 0.);
		if (dotRV < 0.0) return (dotLN * diffuse) * lightColor;

		return (dotLN * diffuse + pow(dotRV, 256.0) * lightColor) * lightColor;

	}

	vec3 illuminate(vec3 o, vec3 p, vec3 nrm){

		const vec3 ambient = vec3(0.01, 0.01, 0.02);
		vec3 final = ambient.rgb;

		vec3 light = vec3(5.0 * sin(u_time), 5.0, 5.0 * cos(u_time));
		vec3 lightColor = vec3(0.5 + sin(u_time * 3.0) * .25 + .25);
		float s1 = shadow(p, light, length(light - p));
		final += getPhong(p, o, light, lightColor, nrm) * s1;


		vec3 light2 = vec3(5.0 * sin(u_time * 2.0), 5.0 * cos(u_time * 2.0), 0.0);
		vec3 lightColor2 = vec3(0.8, 0.0, 0.1);
		float s2 = shadow(p, light2, length(light2 - p));
		final += getPhong(p, o, light2, lightColor2, nrm) * s2;


		return final;

	}

	mat3 getCameraMatrix(vec3 o, vec3 c, float a){

		vec3 k = normalize(c - o);
		vec3 r = vec3(sin(a), cos(a), 0.0);
		vec3 l = normalize(cross(k, r));
		vec3 m = normalize(cross(l, k));
		return mat3(l, m, k);

	}

	float castRay(vec3 o, vec3 d){

		float depth = MIN_DST;

		for (int i = 0; i < MAX_STEPS; i++){

			float dst = sdf(o + depth * d);
			if (dst < EPS) {
				return depth;
			}
			depth += dst;
			if (depth >= MAX_DST){
				return MAX_DST;
			}

		}

		return MAX_DST;

	}

	vec3 render(vec3 o, vec3 d){

		float dst = castRay(o, d);
		vec3 surf = o + dst * d;
		vec3 nrm = getNormal(surf);
		vec3 color = vec3(0.1, 0.1, 0.15);

		if (dst < MAX_DST - EPS) color += illuminate(o, surf, nrm);
		return color;

	}

	void main(void){

		vec2 p = (-u_size.xy + 2.0 * gl_FragCoord.xy) / u_size.y;

		vec3 o = vec3(4.0 * sin(u_time / 10.), 1.8, 4.0 * cos(u_time / 10.));
		vec3 c = vec3(0.0, 0.3, 0.0);
		mat3 ctw = getCameraMatrix(o, c, 0.0);
		vec3 d = ctw * normalize(vec3(p.xy, 6.0));

		vec3 rgb = render(o, d);

		gl_FragColor = vec4(rgb, 1.0);

	}

</script>

<script type="text/javascript">

	//Warehouse
	// vec4(0.9765625, 0.34375, 0.34375, 1.0)

	let ratio = window.devicePixelRatio || 1;
	let canvas = document.querySelector('.vessel');
	canvas.width = canvas.clientWidth * ratio;
	canvas.height = canvas.clientHeight * ratio;
	let gl = canvas.getContext('webgl') || canvas.getContext('experimental-webgl');

	//Geometry
	let vertices = [-1.0, -1.0, 1.0, -1.0, 1.0, 1.0, -1.0, 1.0];
	let vbuffer = gl.createBuffer();
	gl.bindBuffer(gl.ARRAY_BUFFER, vbuffer);
	gl.bufferData(gl.ARRAY_BUFFER, new Float32Array(vertices), gl.STATIC_DRAW);
	// gl.bindBuffer(gl.ARRAY_BUFFER, null);

	//Vertex Shader
	let vshader = gl.createShader(gl.VERTEX_SHADER);
	gl.shaderSource(vshader, document.querySelector('.vsh').textContent);
	gl.compileShader(vshader);

	//Fragment Shader
	let fshader = gl.createShader(gl.FRAGMENT_SHADER);
	gl.shaderSource(fshader, document.querySelector('.fsh').textContent);
	gl.compileShader(fshader);
	if (!gl.getShaderParameter(fshader, gl.COMPILE_STATUS)) throw new Error(gl.getShaderInfoLog(fshader));

	//Program
	let program = gl.createProgram();
	gl.attachShader(program, vshader);
	gl.attachShader(program, fshader);
	gl.linkProgram(program);
	gl.useProgram(program);

	//Geometry to program
	let xy = gl.getAttribLocation(program, 'a_position');
	gl.vertexAttribPointer(xy, 2, gl.FLOAT, false, 0, 0);
	gl.enableVertexAttribArray(xy);

	//Size
	let size = gl.getUniformLocation(program, 'u_size');
	gl.uniform2fv(size, [canvas.width, canvas.height]);

	//Time
	let time = gl.getUniformLocation(program, 'u_time');

	//Mouse tracking
	let u_mouse = gl.getUniformLocation(program, 'u_mouse');
	let mouse = [0,0];

	window.addEventListener('mousemove', (e)=>{
		mouse = [e.clientX * ratio, canvas.height - e.clientY * ratio];
	});
	window.addEventListener('touchmove', (e)=>{
		e.preventDefault();
		mouse = [e.touches[0].clientX * ratio, canvas.height - e.touches[0].clientY * ratio];
	});

	//Render
	function render(){
		requestAnimationFrame(render);
		// gl.clearColor(0.265625, 0.17578125, 0.94921875, 1);
		// gl.clear(gl.COLOR_BUFFER_BIT);


		gl.uniform1f(time, window.performance.now() / 1000);
		gl.uniform2fv(u_mouse, mouse);

		gl.drawArrays(gl.TRIANGLE_FAN, 0, 4);
	}
	render();

</script>
</body>
</html>
