<!DOCTYPE html>
<html>
<head>
        <meta charset="utf-8" />
        <title>Raymarching</title>
        <style>
            body, html {margin:0; width: 100%; height: 100%; min-height: 400px; min-width: 600px; overflow-y: hidden; background: #f00;}
            .vessel {
            	width: 100%;
            	height: 100%;
            }
            .fps {
            	font: 12px/12px 'TT Norms';
            	color: #fff;
            	position: absolute;
            	top: 5px;
            	right: 5px;
            }
        </style>
</head>
<body>

	<canvas class="vessel"></canvas>
	<div class="fps">123</div>

<script class="vsh" type="x-shader/x-fragment">
	
	attribute vec2 a_position;

	void main(void){
		gl_Position = vec4(a_position, 0.0, 1.0);
	}

</script>
<script class="fsh" type="x-shader/x-fragment">

	precision mediump float;
	uniform float u_time;
	uniform vec2 u_size;
	uniform vec2 u_mouse;
	uniform float u_fb;

	const int MAX_STEPS = 256;
	const float MIN_DST = 0.0;
	const float MAX_DST = 100.0;
	const float EPS = 0.0003;

	mat3 rotate(vec3 v, float fAngle)
	{	
		vec4 q = vec4(normalize(v) * sin(fAngle), cos(fAngle));
		vec4 qSq = q * q;
		float xy2 = q.x * q.y * 2.0;
		float xz2 = q.x * q.z * 2.0;
		float yz2 = q.y * q.z * 2.0;
		float wx2 = q.w * q.x * 2.0;
		float wy2 = q.w * q.y * 2.0;
		float wz2 = q.w * q.z * 2.0;
	 
		return mat3 (	
	     qSq.w + qSq.x - qSq.y - qSq.z, xy2 - wz2, xz2 + wy2,
	     xy2 + wz2, qSq.w - qSq.x + qSq.y - qSq.z, yz2 - wx2,
	     xz2 - wy2, yz2 + wx2, qSq.w - qSq.x - qSq.y + qSq.z );
	}


	vec2 julia(vec3 pos){
	    
		vec4 c = vec4(-0.2, -.6, 0.0, 0.0);
	    vec4 z = vec4(pos, 0.0);
		vec4 nz;
	    
		float dr2 = 1.0;
		float r2 = dot(z,z);
		float delta;

		for(int i=0;i<9;i++)
		{
			dr2 *= 4.0*r2;
		    nz.x = z.x * z.x - dot(z.yzw, z.yzw);
			nz.yzw = 2.0*z.x*z.yzw;

			z = nz + c;

			r2 = dot(z,z);
			if(r2>4.0)
	        {
				break;
	        }
		}

		return vec2(0.25*sqrt(r2/dr2)*log(r2), 1.0);
	}



	vec2 sdf(vec3 p){

		vec2 res = julia(p);
		return vec2(res.x, res.y);

	}

	vec3 trace(vec3 o, vec3 d){

		float depth = MIN_DST;
		float closest = 0.0;

		for (int i = 0; i < MAX_STEPS; i++){

			vec2 sdf_res = sdf(o + depth * d);
			float dst = sdf_res.x;
			closest = float(i);

			if (dst < EPS){
				return vec3(depth, 1.0 - closest / float(MAX_STEPS), sdf_res.y);
			}
			depth += dst;
			if (depth > MAX_DST){
				return vec3(MAX_DST, 1.0 - closest / float(MAX_STEPS), sdf_res.y);
			}

		}
		return vec3(MAX_DST, closest/float(MAX_STEPS), 0.0);

	}

	mat3 getCamera(vec3 o, vec3 t, float a){

		vec3 w = normalize(t - o);
		vec3 p = vec3(sin(a), cos(a), 0.0);
		vec3 u = normalize(cross(w, p));
		vec3 v = cross(u, w);
		return mat3(u, v, w);

	}

	vec3 getNormal(vec3 p){
		vec3 eps = vec3(EPS,0.0,0.0);
		return normalize( vec3(
	           sdf(p+eps.xyy).x - sdf(p-eps.xyy).x,
	           sdf(p+eps.yxy).x - sdf(p-eps.yxy).x,
	           sdf(p+eps.yyx).x - sdf(p-eps.yyx).x ) );
	}

	float getShadow(vec3 d, vec3 os){

		float ldist = EPS * 100.;

		for (int i = 0; i < MAX_STEPS; i++){

			float dst = sdf(os + ldist * d).x;
			if (dst < EPS){
				return 0.0;
			}
			ldist += dst;
			if (dst > MAX_DST){
				return 1.0;
			}

		}
		return 0.0;
		
	}

	float getSoftShadow(vec3 os, vec3 light, float s )
	{	
		float maxt = 100.;
	    float f = 1.0;
	    float ph = 1e10;
	    vec3 d = normalize(light - os);
	    float v = EPS * 100.;

	    for(int i = 0; i < MAX_STEPS; i++){

	        float h = sdf(os + d * v).x;
	        if(h < EPS) return 0.0;
	        float y = h*h/(2.0*ph);
	        float sq = sqrt(h*h-y*y);
	        f = min(f, s*sq/max(0.0,v-y));
	        ph = h;
	        v += h;
	        if (v > MAX_DST) break;

	    }
	    return f;
	}

	vec3 render(vec3 o, vec3 d){

		vec3 background = vec3(.0, .1, .16);

		vec3 traced = trace(o, d);
		if (traced.x >= MAX_DST){
			float gc = 1.0 - pow(traced.y, 3.0);
			return background;// - vec3(gc * .2, gc * .18, gc * .1);
		}
		vec3 os = o + traced.x * d;
		vec3 normal = getNormal(os);

		vec3 scolor = mix(vec3(.1, .1, .1), vec3(.2,.2,.2), traced.z);
		//vec3 scolor = vec3(.6,.6,.6);
		//vec3 sund = normalize(vec3(-1.0, 0.6, -0.3));
		vec3 sund = normalize(vec3(cos(u_time / 3.0), sin(u_time / 3.0) * 0.6, -0.3));
		vec3 sunc = vec3(1.0, 0.9, 0.4) * 8.0;
		vec3 fillc = vec3(0.0, 0.1, 0.2);
		vec3 refc = vec3(0.4, 0.3, 0.2);
		//vec3 glowc = vec3(1.0, 0.3, 0.1) * 64.0;

		float ao = clamp(pow(traced.y, 16.) + 0.3, 0.0, 1.0);
		float shadow = getShadow(sund, os);
		float sunv = clamp(dot(normal, sund), 0.0, 1.0);
		float fillv = clamp(0.5 + 0.5 * normal.y, 0.0, 1.0);
		float refv = clamp(dot(normal, normalize(-sund)), 0.0, 1.0);
		float specv = pow(clamp(dot(reflect(d, normal), sund), 0.0, 0.4), 32.);
		//float fresv = getShadow(reflect(d, normal), os) * pow(1.0 - abs(dot(d, normal)), 4.);

		//vec3 final = vec3(0.0);
		vec3 final = sunv * sunc * shadow;
		final += fillv * fillc * ao;
		final += refv * refc * ao;

		final += 1.0 * sunc * specv * shadow;
		//final += fresv * 4. * background;
		//final += glowc * bloom;

		final *= scolor;

		//final = mix(final, background, clamp((traced.x - 1.0) / 48.0, 0.0, 1.0));

		final = pow(final, vec3(0.45));
		return vec3(final);

	}

	void main(void){

		float distance = u_fb;

		vec3 o = vec3(cos(u_mouse.x * 6.28)*distance, (-u_mouse.y + 0.5) * 3., sin(u_mouse.x * 6.28)*distance);
		vec3 target = vec3(0.0, 0.0, 0.0);
		mat3 cam = getCamera(o, target, 0.0);
		vec2 p = (-u_size.xy + 2.0 * gl_FragCoord.xy) / u_size.y;
		vec3 d = cam * normalize(vec3(p.xy, 2.0));

		vec3 color = render(o, d);

		gl_FragColor = vec4(color, 1.0);
		
	}

</script>

<script type="text/javascript">

	//Warehouse
	// vec4(0.9765625, 0.34375, 0.34375, 1.0)

	let fps = document.querySelector('.fps');
	let lt = window.performance.now();

	let ratio = window.devicePixelRatio || 1;
	let canvas = document.querySelector('.vessel');
	canvas.width = canvas.clientWidth * ratio;
	canvas.height = canvas.clientHeight * ratio;
	let gl = canvas.getContext('webgl') || canvas.getContext('experimental-webgl');

	//Geometry
	let vertices = [-1.0, -1.0, 1.0, -1.0, 1.0, 1.0, -1.0, 1.0];
	let vbuffer = gl.createBuffer();
	gl.bindBuffer(gl.ARRAY_BUFFER, vbuffer);
	gl.bufferData(gl.ARRAY_BUFFER, new Float32Array(vertices), gl.STATIC_DRAW);
	// gl.bindBuffer(gl.ARRAY_BUFFER, null);

	//Vertex Shader
	let vshader = gl.createShader(gl.VERTEX_SHADER);
	gl.shaderSource(vshader, document.querySelector('.vsh').textContent);
	gl.compileShader(vshader);

	//Fragment Shader
	let fshader = gl.createShader(gl.FRAGMENT_SHADER);
	gl.shaderSource(fshader, document.querySelector('.fsh').textContent);
	gl.compileShader(fshader);
	if (!gl.getShaderParameter(fshader, gl.COMPILE_STATUS)) throw new Error(gl.getShaderInfoLog(fshader));

	//Program
	let program = gl.createProgram();
	gl.attachShader(program, vshader);
	gl.attachShader(program, fshader);
	gl.linkProgram(program);
	gl.useProgram(program);

	//Geometry to program
	let xy = gl.getAttribLocation(program, 'a_position');
	gl.vertexAttribPointer(xy, 2, gl.FLOAT, false, 0, 0);
	gl.enableVertexAttribArray(xy);

	//Size
	let size = gl.getUniformLocation(program, 'u_size');
	gl.uniform2fv(size, [canvas.width, canvas.height]);

	//Time
	let time = gl.getUniformLocation(program, 'u_time');

	//Mouse tracking
	let u_mouse = gl.getUniformLocation(program, 'u_mouse');
	let mouse = [0,0];

	//Keys
	let u_fb = gl.getUniformLocation(program, 'u_fb');
	let keys = {fb: 4, fb_v: 0, fb_a: 0};

	//Controls
	window.addEventListener('mousemove', (e)=>{
		mouse = [e.clientX * ratio / canvas.width, (canvas.height - e.clientY * ratio) / canvas.height];
	});
	window.addEventListener('touchmove', (e)=>{
		e.preventDefault();
		mouse = [e.touches[0].clientX * ratio, canvas.height - e.touches[0].clientY * ratio];
	});

	window.addEventListener('keydown', (e)=>{
		if (e.which === 87){
			keys.fb_a = -0.005;
		} else if (e.which === 83){
			keys.fb_a = 0.005;
		}
	});
	window.addEventListener('keyup', (e)=>{
		if (e.which === 87 || e.which === 83){
			keys.fb_a = 0;
		}
	});

	//Render
	function render(){
		requestAnimationFrame(render);
		// gl.clearColor(0.265625, 0.17578125, 0.94921875, 1);
		// gl.clear(gl.COLOR_BUFFER_BIT);
		let ctime = window.performance.now();
		fps.innerHTML = Math.round(1000 / (ctime - lt)) + ' fps';
		lt = ctime;

		//Physics
		keys.fb_v += keys.fb_a;
		keys.fb_v *= 0.9;
		keys.fb += keys.fb_v;
		if (keys.fb > 4) {
			keys.fb = 4;
		} else if (keys.fb < 1){
			keys.fb = 1;
		}

		gl.uniform1f(time, ctime / 1000);
		gl.uniform2fv(u_mouse, mouse);
		gl.uniform1f(u_fb, keys.fb);

		gl.drawArrays(gl.TRIANGLE_FAN, 0, 4);
	}
	render();
	
</script>
</body>
</html>